WHITESPACE = _{ WHITE_SPACE+ }

root = @{
    SOI ~ decleration ~ (WHITESPACE ~ decleration)* ~ EOI
}

decleration = !{
    decl_accounts
  | decl_event
}

decl_event = {
    schedule ~ (statements_list | statements_set)
}

statements = {
    statements_list
  | statements_set
  | statements_single
}

statements_list   = { account? ~ ("[" ~ statements_interior ~ "]") }
statements_set    = { account? ~ ("{" ~ statements_interior ~ "}") }
statements_single = { (account_id ~ ">") ~ transaction ~ (">" ~ account_id) ~ (":" ~ label)? }

statements_interior = {
    statements ~ ("," ~ statements)* ~ ","?
}

transaction = {
    (amount ~ trans_mod?)
  | func
}

trans_mod = {
    ("%" ~ account_id?)
  | rate
}

func = {
    "magic!"
}

rate = {
    rate_part ~ "/" ~ rate_part
}

rate_part = {
    "y"
  | "q"
  | "m"
  | "w"
  | "d"
}

amount = @{
    ("+" | "-")? ~ NUMBER ~ ("_"? ~ NUMBER)* ~ ("." ~ NUMBER ~ ("_"? ~ NUMBER)*)?
}

schedule = {
    "(" ~ time_expr ~ ")"
  | &time_function_binary ~ time_function
}

time_expr = _{
    time_function
  | time_func_keyword
  | "(" ~ time_expr ~ ")"
  | time
}

time_function = {
    time_function_binary
  | time_func_not
  | time_func_by
}

time_function_binary = _{
    "(" ~ (time_func_and | time_func_or | time_func_lt | time_func_gt) ~ ")"
}

time_func_and = {
    time_expr ~ ("&" ~ time_expr)+
}
time_func_or  = {
    time_expr ~ ("|" ~ time_expr)+
}

time_func_lt = {
    time_expr ~ "<" ~ time_expr
}

time_func_gt = {
    time_expr ~ ">" ~ time_expr
}

time_func_not = {
    "!" ~ time_expr
}

time_func_by = {
    "by(" ~ time_expr ~ ";" ~ time_expr ~ ")"
}

time_func_keyword = {
    "work"
  | "today"
  | "start"
  | "end"
}

time = {
    date
  | cron
}

date = @{
    NUMBER{4} ~ PUSH("/" | "-" | ".") ~ NUMBER{2} ~ POP ~ NUMBER{2}
}

// TODO: Not the full cron spec
cron = @{
    cron_field ~ WHITESPACE ~ cron_field ~ WHITESPACE ~ cron_field ~ (WHITESPACE ~ cron_field)?
}

cron_field = {
    cron_field_value ~ ("/" | "-" | ",") ~ cron_field_value
  | cron_field_value
}

cron_field_value = {
    "*"
  | NUMBER{1, 2}
}

account = _{
    "<" ~ account_id ~ ">"
}

decl_accounts = {
    "<" ~ account_id ~ ("," ~ account_id)* ~ ">"
}

account_id = @{
    identifier
}

label = {
    string
  | name
}

identifier      = @{ ALPHABETIC ~ (ALPHABETIC | NUMBER)* }
name            = @{ ALPHABETIC ~ (ALPHABETIC | NUMBER | ((!NEWLINE) ~ WHITE_SPACE))* }
string          = ${
    PUSH("\""+) ~ string_interior ~ POP
}
string_interior =  {
    (!(PEEK) ~ ANY)*
}
